plugins {
    id 'java'
    id 'maven-publish'
    id 'signing'
}

apply plugin: 'maven-publish'

group 'com.atatctech'
version '1.0.8'

repositories {
    mavenCentral()
}

dependencies {
    testImplementation 'org.junit.jupiter:junit-jupiter-api:5.8.1'
    testRuntimeOnly 'org.junit.jupiter:junit-jupiter-engine:5.8.1'
    implementation 'com.belerweb:pinyin4j:2.5.1'
    implementation 'com.fasterxml.jackson.core:jackson-core:2.15.2'
    implementation 'com.fasterxml.jackson.core:jackson-databind:2.15.2'
    implementation 'org.jetbrains:annotations:23.0.0'
}

java {
    withJavadocJar()
    withSourcesJar()
}

javadoc {
    options.addStringOption("charset", "UTF-8")
    if (JavaVersion.current().isJava9Compatible()) {
        options.addBooleanOption('html5', true)
    }
}

publishing {
    publications {
        mavenJava(MavenPublication) {
            artifactId = 'packages'
            description = 'Fundamental packages.'
            from components.java

            pom {
                name = 'Packages'
                description = 'Fundamental packages.'
                url = 'https://github.com/ATATCTech/Packages'
                licenses {
                    license {
                        name = 'The Apache License, Version 2.0'
                        url = 'https://www.apache.org/licenses/LICENSE-2.0.txt'
                    }
                }
                developers {
                    developer {
                        id = 'ATATCTech'
                        name = 'ATATCTech'
                        email = 'email@atatctech.com'
                    }
                }
                scm {
                    connection = 'scm:git:git:github.com/ATATCTech/Packages.git'
                    developerConnection = 'scm:git:https://github.com/ATATCTech/Packages.git'
                    url = 'https://github.com/ATATCTech/Packages'
                }
            }
        }
    }
    repositories {
        maven {
            name = "OSSRH"
            if (project.version.toString().endsWith("-SNAPSHOT")) {
                url = "https://s01.oss.sonatype.org/content/repositories/snapshots"
            } else {
                url = "https://s01.oss.sonatype.org/service/local/staging/deploy/maven2/"
            }
            credentials {
                username = findProperty("ossrhUsername") ?: System.getenv("OSSRH_USERNAME")
                password = findProperty("ossrhPassword") ?: System.getenv("OSSRH_PASSWORD")
            }
        }
    }
}

signing {
    sign(publishing.publications.mavenJava)
}


test {
    useJUnitPlatform()
}